machine Heart receives stopEvent {
  vars hr, sysBP, pulseQuality, capillaryRefill;

  init state ObtainHeartVitals {
    entry() {
      hr              = obtain;
      sysBP           = obtain;
      pulseQuality    = obtain;
      capillaryRefill = obtain;

      broadcast HeartMachineUpdate;
      send this, updateHeartMachine;
    }

    on updateHeartMachine do {
      sleep(2);
      goto ObtainHeartVitals;
    }

    on stopEvent do {
      stop;
    }
  }
}

machine ImmuneSystem receives stopEvent {
  var temp;
  init state ObtainImmuneSystemVitals {
    entry {
      temp = obtain;

      broadcast ImmuneSystemUpdate;
      send this, updateImmuneSystem;
    }

    on updateImmuneSystem do {
      sleep(2);
      goto ObtainImmuneSystemVitals;
    }

    on stopEvent do {
      stop;
    }
  }
}

machine Patient {
  var heart              = new Heart();
  var immuneSystem       = new ImmuneSystem();
  var mentalStatus       = obtain;
  var skinCondition      = obtain;
  vars age, highRiskConditions;

  init state PatientData {
    entry(age, highRiskConditions) {
      this.age                = age;
      this.highRiskConditions = highRiskConditions;
    }
  }
}

init machine SepsisScreenMachine {
  vars patient, age, highRiskConditions;

  fun days(age) {
    return age;
  }

  fun months(age) {
    return age * 30;
  }

  fun years(age) {
    return age * 365;
  }

  fun ageInYears(age) {
    return age / 365;
  }

  fun abnormalHeartRate() {
      days(patient.age) in {
        interval(days(0),    months(1)) : return patient.heart.hr > 205;
        interval(months(60), months(3)) : return patient.heart.hr > 205;
        interval(months(3),  years(1))  : return patient.heart.hr > 190;
        interval(years(1),   years(2))  : return patient.heart.hr > 190;
        interval(years(2),   years(4))  : return patient.heart.hr > 140;
        interval(years(4),   years(6))  : return patient.heart.hr > 140;
        interval(years(6),   years(10)) : return patient.heart.hr > 140;
        interval(years(10),  years(13)) : return patient.heart.hr > 100;
        default                         : return patient.heart.hr > 100;
      }
  }

  fun abnormalBloodPressure() {
    days(patient.age) in {
        interval(days(0), days(28))    : return !(patient.heart.sysBP in interval(60, 180));
        interval(days(28), years(1))   : return !(patient.heart.sysBP in interval(70, 180));
        interval(years(1), years(10))  : return !(patient.heart.sysBP in interval(70 + (ageInYears(patient.age) * 2), 180));
        interval(years(10), years(20)) : return !(patient.heart.sysBP in interval(90, 180));
        default                        : return !(patient.heart.sysBP in interval(100, 180));
    }
  }

  fun abnormalTemp() {
    days(patient.age) in {
        interval(days(0), days(60))    : return !(patient.immuneSystem.temp in interval(36, 38));
        interval(days(60), years(20))  : return !(patient.immuneSystem.temp in interval(36, 38));
        default                        : return !(patient.immuneSystem.temp in interval(36, 38));
    }
  }

  fun abnormalPulseQuality() {
    return !(patient.heart.pulseQuality == "Normal");
  }

  fun abnormalMentalStatus() {
    return !(patient.mentalStatus == "Playing/Appropriate");
  }

  fun abnormalCapillaryRefill() {
    return !((patient.heart.capillaryRefill == "1") || (patient.heart.capillaryRefill == "2"));
  }

  fun abnormalSkinColor() {
    return (patient.skinCondition == "Gray") || (patient.skinCondition == "Gray and motted");
  }

  fun highRiskConditions() {
    return !(patient.highRiskConditions == 0);
  }

  init state ObtainAgeAndHighRiskConditions {
    entry {
      instruct("Obtain patient age");
      age = obtain;

      instruct("Obtain high risk conditions");
      highRiskConditions = obtain;

      goto ObtainVitals;
    }
  }

  state ObtainVitals {
    entry {
      patient = new Patient(age, highRiskConditions);
      goto SepsisScreen;
    }
  }

  state SepsisScreen {
    entry {
      var bucket1 = abnormalHeartRate() || abnormalBloodPressure() || abnormalPulseQuality();

      var bucket2 = abnormalTemp();

      var perfusion = abnormalCapillaryRefill() || abnormalSkinColor();
      var bucket3   = abnormalMentalStatus()    || perfusion || highRiskConditions();

      if (bucket1 || bucket2 || bucket3) {
        print("Sepsis suspected \n");
        instruct("Perform sepsis management");
      } else {
        print("Continue regular triage\n");
        instruct("Continue regular triage");
      }

      broadcast stopEvent;
    }
  }
}
